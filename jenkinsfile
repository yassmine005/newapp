def dockerImageServer
def dockerImageClient
def SERVER_CHANGED = false
def CLIENT_CHANGED = false

pipeline {
    agent any

    triggers {
        pollSCM('H/5 * * * *')
    }

    environment {
        DOCKERHUB_CREDENTIALS_ID = '12905163'

        IMAGE_NAME_SERVER = 'yassminemahmoud/mern-server'
        IMAGE_NAME_CLIENT = 'yassminemahmoud/mern-client'
    }

    stages {

        stage('Detect Changes') {
            steps {
                script {
                    def changeLogSets = currentBuild.changeSets

                    for (change in changeLogSets) {
                        for (entry in change.items) {
                            for (file in entry.affectedFiles) {
                                def path = file.path

                                if (path.startsWith("server/")) {
                                    SERVER_CHANGED = true
                                }
                                if (path.startsWith("client/")) {
                                    CLIENT_CHANGED = true
                                }
                            }
                        }
                    }

                    echo "Server changed: ${SERVER_CHANGED}"
                    echo "Client changed: ${CLIENT_CHANGED}"
                }
            }
        }

        stage('Build Server Image') {
            when {
                expression { SERVER_CHANGED }
            }
            steps {
                dir('server') {
                    script {
                        dockerImageServer = docker.build("${IMAGE_NAME_SERVER}:latest")
                    }
                }
            }
        }

        stage('Build Client Image') {
            when {
                expression { CLIENT_CHANGED }
            }
            steps {
                dir('client') {
                    script {
                        dockerImageClient = docker.build("${IMAGE_NAME_CLIENT}:latest")
                    }
                }
            }
        }

        stage('Scan Server Image') {
            when {
                expression { SERVER_CHANGED }
            }
            steps {
                script {
                    bat """
                    docker run --rm -v /var/run/docker.sock:/var/run/docker.sock aquasec/trivy:latest image --exit-code 0 --severity LOW,MEDIUM,HIGH,CRITICAL ${IMAGE_NAME_SERVER}:latest
                    """
                }
            }
        }

        stage('Scan Client Image') {
            when {
                expression { CLIENT_CHANGED }
            }
            steps {
                script {
                    bat """
                    docker run --rm -v /var/run/docker.sock:/var/run/docker.sock aquasec/trivy:latest image --exit-code 0 --severity LOW,MEDIUM,HIGH,CRITICAL ${IMAGE_NAME_CLIENT}:latest
                    """
                }
            }
        }

        stage('Push Images to Docker Hub') {
            when {
                expression { SERVER_CHANGED || CLIENT_CHANGED }
            }
            steps {
                script {
                    docker.withRegistry('', DOCKERHUB_CREDENTIALS_ID) {
                        if (SERVER_CHANGED) dockerImageServer.push('latest')
                        if (CLIENT_CHANGED) dockerImageClient.push('latest')
                    }
                }
            }
        }
    }

    post {
        always {
            echo "Cleaning unused Docker imagesâ€¦"

            bat """
            docker system prune -af
            """

            echo "Cleanup complete!"
        }
    }
}
